<?xml version="1.0" encoding="UTF-8" ?>
<!-- =============================================================================
     WSDL-Viz    
     WSDL-Viz allows you to see the content of a Web Service WSDL in a visual way.
                   
     Copyright Nawwar Kabbani, 2012
     http://nawwar.net
     Published under GPL version 2.0.          
	 =============================================================================
  -->

<xsl:stylesheet xmlns:xsl="http://www.w3.org/1999/XSL/Transform"
	xmlns:xsd="http://www.w3.org/2001/XMLSchema" version="1.0"
	xmlns:wsdl="http://schemas.xmlsoap.org/wsdl/">
	<xsl:param name="current-date" ></xsl:param>
	<xsl:param name="filename"></xsl:param>
	<xsl:output method="html" indent="yes" omit-xml-declaration="no"
		 media-type="text/html" />

	<xsl:variable name="version">0.1.0</xsl:variable>
	

	<xsl:template match="/wsdl:definitions">
		<xsl:text disable-output-escaping='yes'>&lt;!DOCTYPE html&gt;</xsl:text>
		<html>
			<head>
				<style type="text/css">
					body, p, h1, h2, h3, h4, table, td, th {
					    font-family: verdana,helvetica,arial,sans-serif;
					    font-size:small;
					    background-color:White;
					}
					table { border-collapse:collapse; border:1px  }
					table td, th { border:1px solid gray; padding:5px;}
					table th { background-color:#d5d5e0; font-weight:bold;}
					.simpleType td { border:0px; }
					.docText {color: #60A060; font-style: italic; display:none;}
					.footer {
					    border-top: 2px dotted gray;
    					width: -moz-max-content;
    					margin-top: 20px;
					}
					
				</style>

				<script src="http://code.jquery.com/jquery-latest.js"></script>

				<script type="text/javascript">


				function showHelp(element) {
					element.next(".docText").toggle();
				}
				
				function toggleElement(element) {
					typeName = element.text();
					typeView = $('#'+typeName);
					if (typeView.length) {
						typeView2 = typeView.clone();
						element.after(typeView2);
						element.parent().css("padding", "0px");
						element.hide();
						typeView2.show(300);
					}
				}
				
				$(function() {
					$(document).click (function(e) {
						element = $(e.target)
						if (element.hasClass('TypeLink')) {
							toggleElement(element); return false;
						}
						else if (element.hasClass('docIcon') ) {
							showHelp(element); return false;	
						}
						else {
							return true;
						}
					});
					
					
				});

				</script>
			</head>
			<body>
			
			<h1 >
			<xsl:value-of select="@name" />
			(<xsl:value-of select="$filename" />)
			</h1>
			<div class="documentation">
			<xsl:value-of select="wsdl:documentation" />
			<br />
			<xsl:value-of select="wsdl:service/wsdl:documentation" />
			</div>
			
			<xsl:apply-templates select="*" />

			<div class="footer">
			<xsl:if test="$current-date" >
			Generated by 
			<a href="http://neo00.github.com/WSDL-Viz/">WSDL-Viz</a> 
			(v.<xsl:value-of select="$version" />) on
			<xsl:value-of select="$current-date" />
			</xsl:if>
			</div>
			</body>
		</html>

	</xsl:template>
	
	<xsl:template match="wsdl:service" />
	<xsl:template match="xsd:schema/xsd:annotation" />
	<xsl:template match="wsdl:documentation" />
	
	<xsl:template match="wsdl:message" mode="message">
	<table style="width:100%">
			<xsl:apply-templates select="wsdl:part" mode="message" />
	</table>
	</xsl:template>
	
	<xsl:template match="wsdl:part[@element]" mode="message">
		<xsl:variable name="messageType" select="substring-after(@element, ':')" />
	 <tr>
		<td><xsl:value-of select="@name" /></td>
		<td><a href="#" class="TypeLink">
				<xsl:value-of select="$messageType" />
			</a>
		</td>
	 </tr>
	</xsl:template>
	<xsl:template match="wsdl:part[@type]" mode="message">
		<xsl:variable name="messageType" select="substring-after(@type, ':')" />
	 <tr>
		<td><xsl:value-of select="@name" /></td>
		<td><a href="#" class="TypeLink">
				<xsl:value-of select="$messageType" />
			</a>
		</td>
	 </tr>
	</xsl:template>
	
	<xsl:template match="wsdl:portType/wsdl:operation" >
		<hr />
		<table>
			<tr>
		 		<th colspan="2" align="center">
					<xsl:value-of select="@name"></xsl:value-of>
				</th>
			</tr>
			<tr>
				<td colspan="2" align="left" class="documentation">
					<xsl:value-of select="wsdl:documentation"></xsl:value-of>
				</td>
			</tr>
			<tr>
				<td>Input</td>
				<td style="padding:0px">
					<xsl:variable name="input"
						select="substring-after(wsdl:input/@message, ':')" />
					<xsl:apply-templates select="//wsdl:message[@name=$input]" mode="message"/>
				</td>
			</tr>
			<tr>
				<td>Output</td>
				<td style="padding:0px">
					<xsl:variable name="type"
						select="substring-after(wsdl:output/@message, ':')" />
					<xsl:apply-templates select="//wsdl:message[@name=$type]" mode="message"/>

				</td>
			</tr>
			<tr>
				<td>Fault</td>
				<td style="padding:0px">
					<xsl:variable name="type"
						select="substring-after(wsdl:fault/@message, ':')" />
					<xsl:apply-templates select="//wsdl:message[@name=$type]" mode="message"/>

				</td>
			</tr>
		</table> 
	</xsl:template>
	
	<xsl:template match="xsd:complexType[count(xsd:choice)=0]">
		<table style="display:none;width:100%" class="complexType">
			<xsl:attribute name="id">
				<xsl:value-of select="@name" />
			</xsl:attribute>
			<tr>
				<th colspan="3" align="center">
					<a>
						<xsl:attribute name="name">
							<xsl:value-of select="@name"></xsl:value-of>
						</xsl:attribute>
					</a>
					<xsl:value-of select="@name"></xsl:value-of>
 	  				<xsl:apply-templates select="xsd:annotation" mode="documentation" />
				</th>
			</tr>
			
			<xsl:apply-templates select="xsd:complexContent|xsd:extension|xsd:sequence|xsd:all" />
		</table>
	</xsl:template>
	
	<xsl:template match="xsd:complexType/xsd:choice">
		<table style="display:none;width:100%;background-color:red">
			<xsl:attribute name="id">
				<xsl:value-of select="../@name" />
			</xsl:attribute>
			<tr>
				<th colspan="3" align="center">
					<a>
						<xsl:attribute name="name">
							<xsl:value-of select="../@name"></xsl:value-of>
						</xsl:attribute>
					</a>
					<xsl:value-of select="../@name"></xsl:value-of>
					<xsl:if test="../xsd:annotation/xsd:documentation">
						<p colspan="3" align="left" style="color: #74B374; font-style: italic">
							<xsl:value-of select="../xsd:annotation/xsd:documentation"></xsl:value-of>
						</p>
					</xsl:if>
					<br />&lt;Choice&gt;	
				</th>
			</tr>
			<xsl:apply-templates select="xsd:element" mode="element"/>
		</table>
	
	</xsl:template>

	<xsl:template match="xsd:all|xsd:sequence">
		<xsl:apply-templates select="xsd:element" mode="element"/>
	</xsl:template>
	

	<xsl:template match="xsd:complexContent/xsd:extension">
		<xsl:apply-templates />
		<tr>
			<td colspan="3">
				<xsl:apply-templates select="xsd:annotation" mode="element" />
				<div style="font-style:italic">Base type:</div>
				<xsl:variable name="type" select="substring-after(@base, ':')" />
				<a href="#" class="TypeLink">
					<xsl:value-of select="$type" />
				</a>
			</td>
		</tr>
	</xsl:template>

	<xsl:template match="xsd:element" mode="element">
		<xsl:call-template name="elementTemplate" />
	</xsl:template>
	<xsl:template name="elementTemplate">
	
		<tr>
			<td>
				<xsl:value-of select="@name" />
				<xsl:apply-templates select="xsd:annotation" />
			</td>
			<td>
				<xsl:choose>
					<xsl:when test="@minOccurs = 0">
						O
					</xsl:when>
					<xsl:when test="@minOccurs = 1">
						M
					</xsl:when>
					<xsl:when test="@minOccurs > 1">
						<xsl:value-of select="@minOccurs" />
					</xsl:when>
					<xsl:otherwise>
						M
					</xsl:otherwise>
				</xsl:choose>
				<xsl:choose>
					<xsl:when test="@maxOccurs = 'unbounded'">
						, &#8734;
					</xsl:when>
					<xsl:when test="@maxOccurs > 1">
						,
						<xsl:value-of select="@maxOccurs" />
					</xsl:when>
				</xsl:choose>
			</td>
			<td>
				<xsl:variable name="type" select="substring-after(@type, ':')" />
				<a href="#" class="TypeLink">
					<xsl:value-of select="$type" />
				</a>
			</td>
		</tr>
	</xsl:template>
	
<!-- 	<xsl:template match="xsd:annotation" >
	<xsl:apply-templates select="xsd:documentation" />
	</xsl:template>
 -->
	<xsl:template match="xsd:element">
		<table style="display:none;width:100%">
			<xsl:attribute name="id">
				<xsl:value-of select="@name" />
			</xsl:attribute>
			<tr>
				<td>
					<xsl:value-of select="@name" />
					<xsl:apply-templates select="xsd:annotation"  />
				</td>
				<td>
					<xsl:variable name="type" select="substring-after(@type, ':')" />
					<a href="#" class="TypeLink">
						<xsl:value-of select="$type" />
					</a>
				</td>
			</tr>
		</table>

	</xsl:template>

	<xsl:template match="xsd:simpleType">
		<table style="display:none;width:100%">
			<xsl:attribute name="id">
				<xsl:value-of select="@name" />
			</xsl:attribute>
			<xsl:apply-templates select="xsd:restriction" />

		</table>
	</xsl:template>

 	<xsl:template match="xsd:restriction[xsd:enumeration]">
				<tr ><th  align="center">
					<a>
						<xsl:attribute name="name">
							<xsl:value-of select="../@name"></xsl:value-of>
						</xsl:attribute>
					</a>
					<xsl:value-of select="../@name"></xsl:value-of>
					<div><xsl:text>[enum]</xsl:text></div>
					<xsl:apply-templates select="../xsd:annotation" /> 
				</th>
				</tr>
				<xsl:apply-templates select="xsd:enumeration" />
	</xsl:template>

  	<xsl:template match="xsd:restriction[count(xsd:enumeration) = 0]">
	<tr class="simpleType"><td>
		<a>
			<xsl:attribute name="name">
				<xsl:value-of select="../@name"></xsl:value-of>
			</xsl:attribute>
		</a>
			<xsl:value-of select="../@name"></xsl:value-of>
			<xsl:apply-templates select="../xsd:annotation" />				
		</td>
		<td align="right" >
		<xsl:text disable-output-escaping="yes">&amp;nbsp;:&amp;nbsp; </xsl:text>
		
		<xsl:variable name="type" select="substring-after(@base, ':')" />
		<a href="#" class="TypeLink">
			<xsl:value-of select="$type" />
		</a>
	</td></tr>
	</xsl:template>
 
	<xsl:template match="xsd:documentation">
		<a href="#" class="docIcon">[?]</a><div class="docText">
		<xsl:value-of select="text()" />
		</div>
	</xsl:template>
	
	<xsl:template match="xsd:enumeration">
		<tr>
			<td colspan="3">
				<xsl:value-of select="@value" />
			</td>
		</tr>
	</xsl:template>
	
	
	

<!-- 	<xsl:template match="@*|node()">
		<xsl:apply-templates select="node()" />
	</xsl:template>
 -->	
	<xsl:template match="xsd:import">
		<xsl:variable name="schema" select="@schemaLocation" />
		<xsl:apply-templates select="document($schema)" />

	</xsl:template>


</xsl:stylesheet>